# Scroll down and hit blue arrow down to run and see the results + the sources
SELECT ?year ?item ?itemLabel (MAX(?number) AS ?passengers)
  (SAMPLE(COALESCE(?reference_URL, ?monthly_reference_URL2)) AS ?sample_reference_URL)
WITH
{
  SELECT ?item ?statement ?date ?year ?timevalue ?numberperperiod ?reference_URL
  WHERE
  {
    ?item wdt:P238 ?airport_code
    VALUES ?airport_code 
    {
 "CGN"
    }
    ?item p:P3872 ?statement.
    ?statement pqv:P585 ?timevalue;
               ps:P3872 ?numberperperiod.
    ?timevalue wikibase:timeValue ?date.
    OPTIONAL { ?statement pq:P518 ?applies. }
    OPTIONAL { ?statement prov:wasDerivedFrom / (pr:P854|pr:P4656) ?reference_URL. }
    FILTER (BOUND(?applies)=false || ?applies = wd:Q2165236 )
    MINUS { ?statement wikibase:rank wikibase:DeprecatedRank }
    BIND (YEAR(?date) AS ?year)
    FILTER (?year >1949).
    FILTER (?year < YEAR(NOW()))
  }
} AS %airport
WHERE
{
  {
    # Get the sum of monthly values within a year
    SELECT ?item ?year (SUM(?max_numberperperiod) AS ?number) (SAMPLE(?monthly_reference_URL) AS ?monthly_reference_URL2)
    WHERE
    {
      # Get the maximal value and a sample reference URL for each unique month
      {
        SELECT ?item ?year (MAX(?numberperperiod) AS ?max_numberperperiod) (SAMPLE(?reference_URL) AS ?monthly_reference_URL)
        WHERE
        {
          INCLUDE %airport
          ?timevalue wikibase:timePrecision ?prec.
          FILTER (?prec > 9)# precision more precise or equal to month
        }
        GROUP BY ?item ?year ?date
      }
    }
    GROUP BY ?item ?year
  }
  UNION
  {
    ?timevalue wikibase:timePrecision 9 .
    BIND (?numberperperiod AS ?number)
    BIND (?reference_URL AS ?sample_reference_URL)
    INCLUDE %airport
  }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}
GROUP BY ?item ?itemLabel ?year
ORDER BY ?item DESC (?year)

##SAMPLEQUERY##

SELECT ?year ?item ?itemLabel (MAX(?number) AS ?passengers)
(SAMPLE(COALESCE(?reference_URL, ?monthly_reference_URL2)) AS ?sample_reference_URL)
WITH
{
SELECT ?item ?statement ?date ?year ?timevalue ?numberperperiod ?reference_URL
WHERE
{
?item wdt:P238 ?airport_code
VALUES ?airport_code 
{
"FCO" "CIA"
}
?item p:P3872 ?statement.
?statement pqv:P585 ?timevalue;
           ps:P3872 ?numberperperiod.
?timevalue wikibase:timeValue ?date.
OPTIONAL { ?statement pq:P518 ?applies. }
OPTIONAL { ?statement prov:wasDerivedFrom / (pr:P854|pr:P4656) ?reference_URL. }
FILTER (BOUND(?applies)=false || ?applies = wd:Q2165236 )
MINUS { ?statement wikibase:rank wikibase:DeprecatedRank }
BIND (YEAR(?date) AS ?year)
FILTER (?year >1949).
FILTER (?year < YEAR(NOW()))
}
} AS %airport
WHERE
{
{
# Get the sum of monthly values within a year
SELECT ?item ?year (SUM(?max_numberperperiod) AS ?number) (SAMPLE(?monthly_reference_URL) AS ?monthly_reference_URL2)
WHERE
{
  # Get the maximal value and a sample reference URL for each unique month
  {
    SELECT ?item ?year (MAX(?numberperperiod) AS ?max_numberperperiod) (SAMPLE(?reference_URL) AS ?monthly_reference_URL)
    WHERE
    {
      INCLUDE %airport
      ?timevalue wikibase:timePrecision ?prec.
      FILTER (?prec > 9)# precision more precise or equal to month
    }
    GROUP BY ?item ?year ?date
  }
}
GROUP BY ?item ?year
}
UNION
{
?timevalue wikibase:timePrecision 9 .
BIND (?numberperperiod AS ?number)
BIND (?reference_URL AS ?sample_reference_URL)
INCLUDE %airport
}
SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}
GROUP BY ?item ?itemLabel ?year
ORDER BY ?item DESC (?year)

##SAMPLEQUERY##

SELECT ?organisation ?organisationLabel 
(GROUP_CONCAT(DISTINCT ?officialName; SEPARATOR = ",") AS ?officialNames) 
(GROUP_CONCAT(DISTINCT ?typeLabel; SEPARATOR = ",") AS ?types) 
(GROUP_CONCAT(DISTINCT ?industryLabel; SEPARATOR = ",") AS ?industries) 
(GROUP_CONCAT(DISTINCT ?inception; SEPARATOR = ",") AS ?inceptions) 
(GROUP_CONCAT(DISTINCT ?CEOLabel; SEPARATOR = ",") AS ?CEOs) 
(GROUP_CONCAT(DISTINCT ?founderLabel; SEPARATOR = ",") AS ?founders)
(GROUP_CONCAT(DISTINCT ?countryLabel; SEPARATOR = ",") AS ?countries)
(GROUP_CONCAT(DISTINCT ?HQLabel; SEPARATOR = ",") AS ?HQs) 
(GROUP_CONCAT(DISTINCT ?cityLabel; SEPARATOR = ",") AS ?cities) 
(GROUP_CONCAT(DISTINCT ?parent; SEPARATOR = ",") AS ?parents) 
(GROUP_CONCAT(DISTINCT ?parentLabel; SEPARATOR = ",") AS ?parentsLabel) 
(GROUP_CONCAT(DISTINCT ?child; SEPARATOR = ",") AS ?children) 
(GROUP_CONCAT(DISTINCT ?childLabel; SEPARATOR = ",") AS ?childrenLabel) 
(GROUP_CONCAT(DISTINCT ?legalFormLabel; SEPARATOR = ",") AS ?legalForms) 
(GROUP_CONCAT(DISTINCT ?website; SEPARATOR = ",") AS ?websites) 
(GROUP_CONCAT(DISTINCT ?ownerof; SEPARATOR = ",") AS ?ownerofs) 
(GROUP_CONCAT(DISTINCT ?ownerofLabel; SEPARATOR = ",") AS ?ownerofsLabel) 
(GROUP_CONCAT(DISTINCT ?ownedby; SEPARATOR = ",") AS ?ownedbys) 
(GROUP_CONCAT(DISTINCT ?ownedbyLabel; SEPARATOR = ",") AS ?ownedbysLabel)
?logo
?isni
?isin
?grid_id
?le_id
?ringgold_id
?ror_id
?h2020_id
?vat_id
WHERE {
SERVICE wikibase:label {
bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en,fr,de,ru,pl,ar,zh,es,cs,nl,no,nb,he,bn,sl,ro,fa,hu,bg,cy,sv,ja,da,tr,pt".
?organisation rdfs:label ?organisationLabel.
?type rdfs:label ?typeLabel.
?industry rdfs:label ?industryLabel.
?country rdfs:label ?countryLabel.
?CEO rdfs:label ?CEOLabel.
?founder rdfs:label ?founderLabel.
?HQ rdfs:label ?HQLabel.
?city rdfs:label ?cityLabel.
?parent rdfs:label ?parentLabel.
?child rdfs:label ?childLabel.
?legalForm rdfs:label ?legalFormLabel.
?owner rdfs:label ?ownerofLabel.        
?ownedby rdfs:label ?ownedbyLabel.
}
BIND(<http://www.wikidata.org/entity/Q30280412> AS ?organisation).
  OPTIONAL { ?organisation wdt:P1448 ?officialName. }
  OPTIONAL { ?organisation wdt:P31 ?type. }
  OPTIONAL { ?organisation wdt:P452 ?industry. }
  OPTIONAL { ?organisation wdt:P571 ?inception. }
  OPTIONAL { ?organisation wdt:P169 ?CEO. }
  OPTIONAL { ?organisation wdt:P112 ?founder. }
  ?organisation wdt:P17 ?country. 
  OPTIONAL { ?organisation wdt:P159 ?HQ. }
  OPTIONAL { ?organisation wdt:P131 ?city. }
  OPTIONAL { ?organisation wdt:P749 ?parent. }
  OPTIONAL { ?organisation wdt:P355 ?child. }
  OPTIONAL { ?organisation wdt:P1454 ?legalForm. }
  OPTIONAL { ?organisation wdt:P856 ?website. }
  OPTIONAL { ?organisation wdt:P154 ?logo. }
  OPTIONAL { ?organisation wdt:P213 ?isni. }
  OPTIONAL { ?organisation wdt:P946 ?isin. }
  OPTIONAL { ?organisation wdt:P1830 ?ownerof. }
  OPTIONAL { ?organisation wdt:P127 ?ownedby. }
  OPTIONAL { ?organisation wdt:P2427 ?grid_id. }
  OPTIONAL { ?organisation wdt:P1278 ?le_id. }          
  OPTIONAL { ?organisation wdt:P3500 ?ringgold_id. }
  OPTIONAL { ?organisation wdt:P1278 ?ror_id. }
  OPTIONAL { ?organisation wdt:P5785 ?h2020_id. }
  OPTIONAL { ?organisation wdt:P3608 ?vat_id. }
}
GROUP BY ?organisation ?organisationLabel ?officialNames ?types ?industries ?inceptions ?CEOs ?founders 
?countries ?HQs ?cities ?parents ?parentsLabel ?children ?childrenLabel ?legalForms ?websites ?logo
?isni ?isin ?grid_id ?le_id ?ror_id ?ringgold_id ?h2020_id ?vat_id ?ownerofs ?ownerofsLabel ?ownedbys 
?ownedbysLabel

##SAMPLEQUERY##

SELECT (?item AS ?url) (COALESCE(?title, "") AS ?title) (COALESCE(?idIMDB, "N/A") AS ?idIMDB) (COALESCE(?idRT, "N/A") AS ?idRT) (COALESCE(?idMETA, "N/A") AS ?idMETA) WHERE { ?item p:P1874/ps:P1874 "80126440". OPTIONAL {?item p:P1476/ps:P1476 ?title.}  OPTIONAL {?item p:P345/ps:P345 ?idIMDB.} OPTIONAL {?item p:P1258/ps:P1258 ?idRT.} OPTIONAL {?item p:P1712/ps:P1712 ?idMETA.} } LIMIT 1
        
        
        
        
        
        
        
